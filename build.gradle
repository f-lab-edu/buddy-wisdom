// gradle 에서 yml 을 읽어오기 위한 설정
import com.fasterxml.jackson.databind.ObjectMapper
import com.fasterxml.jackson.dataformat.yaml.YAMLFactory
// compile time 이 아닌 runtime 시에 동작하므로 buildscript 에 의존성 추가
// Gradle 초기 설정 단계에서 클래스 경로가 설정되어야 하므로 buildscript 블록은 스크립트 파일의 상단에 위치
buildscript {
    dependencies {
        classpath 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
        classpath 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.15.0'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'jacoco' // jacoco 플러그인 추가
}

//// jacoco 설정 시작
jacoco {
    // JaCoCo 버전 (0.8.8 이상부터 java 17 지원)
    toolVersion = '0.8.8'

//  테스트결과 리포트를 저장할 경로 변경
//  default 는 "$/jacoco"
//  reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        // 원하는 리포트를 켜고 끌 수 있습니다.
        html.enabled true
        xml.enabled false
        csv.enabled false

//  각 리포트 타입 마다 리포트 저장 경로를 설정할 수 있습니다.
        html.destination file("$buildDir/jacocoHtml")
//  xml.destination file("$buildDir/jacoco.xml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
        }
    }
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
}
//// jacoco 설정 끝


group = 'cobook'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

// submodule
sourceSets {
    main {
        java {
            srcDir 'buddy-wisdom-private'
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // spring security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    // validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // jdbc
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc:3.0.4'
    // mybatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.0'
    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.1.0'
    // db
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'
    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // jwt
    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    // Jackson yaml
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.15.0'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.1'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
}

// gradle 에서 submodule 의 yml 을 읽어오기 위한 설정
tasks.register('readYamlFile') {
    doLast {
        File file = new File('buddy-wisdom-private/application.yml')
        ObjectMapper mapper = new ObjectMapper(new YAMLFactory())
        def config = mapper.readValue(file, Map.class)
        println config
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    enabled = false
}